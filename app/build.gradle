apply plugin: 'com.android.application'

/**采用解析Properties文件获取签名信息的方式，把签名放进版本控制不是明智的做法*/
Properties props = new Properties();
props.load(new FileInputStream(file("signing.properties")))
android {
    signingConfigs {//签名文件配置
        config {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['STORE_FILE'])
            storePassword props['STORE_PASSWORD']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ruiqin.productflavorsdemo"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.config
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {//编译类型
        release {
            minifyEnabled false//是否开启混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /*为apk自动替换生成新名字，格式如： [項目名]_[test/release]_[version]_[date].apk*/
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "pda-${variant.name}-v${variant.versionName}.apk"
        }
    }

    flavorDimensions "default"
    productFlavors {
        beijing {
            versionCode 3
            versionName "1.0.3"
            applicationIdSuffix ".bj"
            manifestPlaceholders = [App_Name: "beijing haha"]
            buildConfigField "String", "App_Name", '"北京haha"'
        }

        shenzhen {
            versionCode 5
            versionName "1.0.5"
            applicationIdSuffix ".sz"
            manifestPlaceholders = [App_Name: "shenzhen haha"]
            buildConfigField "String", "App_Name", '"深圳haha"'
        }

        guangzhou {
            versionCode 5
            versionName "1.0.5"
            applicationIdSuffix ".gz"
            manifestPlaceholders = [App_Name: "guangzhou haha"]
            buildConfigField "String", "App_Name", '"广州haha"'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:27.1.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}

